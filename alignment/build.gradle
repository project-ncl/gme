plugins {
    id 'maven-publish' // only used for publishing to local maven
    id 'java-gradle-plugin' // applies "java" plugin and adds gradleApi() to dependencies
}

group 'org.jboss.gm.alignment'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
    compile 'org.apache.commons:commons-lang3:3.8.1'
    compile 'commons-io:commons-io:2.6'
    testCompile 'junit:junit:4.12'
    testCompile'org.assertj:assertj-core:3.12.2'
}

gradlePlugin {
    plugins {
        alignmentPlugin {
            id = 'gm-alignment'
            implementationClass = 'org.jboss.gm.alignment.AlignmentPlugin'
        }
    }
}

// separate source set and task for functional tests

sourceSets {
    functionalTest {
        java.srcDir file('src/functTest/java')
        resources.srcDir file('src/functTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task functionalTest(type: Test) {
    description = 'Runs functional tests'
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    mustRunAfter test
}

check.dependsOn functionalTest

task install {
    description 'Publishes to maven local repository'
    dependsOn publishToMavenLocal
}
